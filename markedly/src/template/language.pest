template = ${ soi ~ component* ~ eoi }

// Components
component = { indentation ~ identifier ~ style_class? ~ spacing* ~ (attributes)? ~ (spacing* ~ newline)+ }
indentation = { spacing* }
style_class = { "." ~ identifier }

// Attributes
attributes = !{ "{" ~ (key_value ~ ("," ~ key_value)* ~ ","?)? ~ "}" }
key_value = { identifier ~ ":" ~ value }
value = { string | percentage | float | integer | tuple | default }

// Identifiers
identifier_char = _{ 'a'..'z' | 'A'..'Z' | '0'..'9' | "_" | "-" }
identifier = { identifier_char+ }

// Values
tuple = { "(" ~ value ~ ("," ~ value)* ~ ")" }

string = @{ "\"" ~ (escape | !("\"" | "\\") ~ any)* ~ "\"" }
escape = @{ "\\" ~ ("\"" | "\\" ) }

integer = @{ "-"? ~ number }
float = @{ "-"? ~ number ~ "." ~ ('0'..'9'+)? }
percentage = @{ "-"? ~ number ~ "%" }
number = { "0" | ('1'..'9' ~ '0'..'9'*) }

default = @{ "default" }

// Whitespace
spacing = _{ " " | "\t" }
newline = { "\r" | "\n" }
whitespace = _{ spacing | newline }
